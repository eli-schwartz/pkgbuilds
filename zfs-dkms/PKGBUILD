# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Iacopo Isimbaldi <isiachi@rhye.it>

# All my PKGBUILDs are managed at https://github.com/eli-schwartz/pkgbuilds

pkgname=zfs-dkms
pkgver=2.1.0
pkgrel=2
pkgdesc="Kernel modules for the Zettabyte File System."
arch=('any')
url="https://zfsonlinux.org/"
license=('CDDL')
conflicts=('spl-dkms')
provides=("ZFS-MODULE=${pkgver}" "SPL-MODULE=${pkgver}" 'spl-dkms')
# ambiguous, provided for backwards compat, pls don't use
provides+=('zfs')
replaces=('spl-dkms')
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"{,.asc}
        https://github.com/openzfs/zfs/commit/1b06b03a7be88f4565c2c3114b8d3f2dc9f9408a.patch
        https://github.com/openzfs/zfs/commit/1c24bf966c373009f2be77438e8696aabf50a7e7.patch
        "0001-only-build-the-module-in-dkms.conf.patch")
sha256sums=('60bddcb630284c771dcf5a1109ca9329d16523a3a40a374019955cfbf97bf34f'
            'SKIP'
            'daf0e6dd4ba29cc46c943252316d0668121846462c47f10226e0fa4708ef9eff'
            'd439d54813b0e4f53a597c6b3e3f51173b29f58bfe00e40bb3a492200e97a3b4'
            '780e590383fb00389c5e02ac15709b7a476d9e07d3c4935ed9eb67c951a88409')
b2sums=('f7cc1cde711c255bacb000207b8e9a594105d18601a503d75a2a3055ed29ca941e148492fe52809f81a250848cfb5868b3d1c42860464a3d7d744a416db08929'
        'SKIP'
        '45f4eaeb0854e88baaf2b8df9bca2b0b21c0c0b87089d497c135243f9709c4111348ce4500c8d036a266bda7159e2cf064f73d6a77548a3c172c41918d1d977f'
        '24cab0e297e67f04e9c96459ece8a0e9de78943d17dc4ce90eee2925e8fc8be98bc8ce0a08559a95aba4ac6f4c6f3f2d32270cf791019ca8c2b19e4de7107285'
        '1fdae935043d979b9241f07f8baa25a9a0367c24c31c84a59dfe8d6b468a523d8f49b68da3c7fd3194db6638f9d7bef046fc5e2669ce25d73c65009c16bf6c50')
validpgpkeys=('4F3BA9AB6D1F8D683DC2DFB56AD860EED4598027'  # Tony Hutter (GPG key for signing ZFS releases) <hutter2@llnl.gov>
              'C33DF142657ED1F7C328A2960AB9E991C6AF658B') # Brian Behlendorf <behlendorf1@llnl.gov>

prepare() {
    cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

    patch -p1 -i ../0001-only-build-the-module-in-dkms.conf.patch

    # 5.14 compat
    patch -p1 -i ../1b06b03a7be88f4565c2c3114b8d3f2dc9f9408a.patch
    patch -p1 -i ../1c24bf966c373009f2be77438e8696aabf50a7e7.patch

    # remove unneeded sections from module build
    sed -ri "/AC_CONFIG_FILES/,/]\)/{
/AC_CONFIG_FILES/n
/]\)/n
/^\s*(module\/.*|${pkgname%-dkms}.release|Makefile)/!d
}" configure.ac

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

    ./scripts/dkms.mkconf -n ${pkgname%-dkms} -v ${pkgver} -f dkms.conf
}

package() {
    depends=("zfs-utils=${pkgver}" 'dkms')

    cd "${srcdir}"/${pkgname%-dkms}-${pkgver}

    dkmsdir="${pkgdir}/usr/src/${pkgname%-dkms}-${pkgver}"
    install -d "${dkmsdir}"/{config,scripts}
    cp -a configure dkms.conf Makefile.in META ${pkgname%-dkms}_config.h.in ${pkgname%-dkms}.release.in include/ module/ "${dkmsdir}"/
    cp config/compile config/config.* config/missing config/*sh "${dkmsdir}"/config/
    cp scripts/enum-extract.pl scripts/dkms.postbuild "${dkmsdir}"/scripts/
}
